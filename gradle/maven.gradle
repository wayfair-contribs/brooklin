allprojects {
  version = "1.0.2-11-SNAPSHOT"
}

subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'pegasus'

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
  }

  task dataTemplateSourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.mainGeneratedDataTemplate.allJava
  }

  task testDataTemplateSourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.testGeneratedDataTemplate.allJava
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  task testJar(type: Jar) {
    from sourceSets.test.allJava
    classifier = "tests"
  }

  def allPublications = ['mavenJava', 'dataTemplateJava']
  if (project.tasks.findByName("mainRestClientJar")) {

    task restClientSourceJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.mainGeneratedRest.allJava
    }

    publishing {
      publications {
        restClientJava(MavenPublication) {
          artifactId project.name + "-rest-client"
          artifact mainRestClientJar
          artifact restClientSourceJar
          artifact javadocJar

          version project.version
        }
      }
    }

    allPublications.add('restClientJava')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourcesJar
        artifact javadocJar
        artifact testJar

        version project.version

        // we strive to meet https://central.sonatype.org/pages/requirements.html
        pom {
          name = 'Brooklin'
          description = 'An extensible distributed system for reliable nearline data streaming at scale'
          url = 'https://github.com/linkedin/brooklin'

          // Default packaging is jar, so only specify otherwise for non-jars
          if (project.pluginManager.hasPlugin('war')) {
            packaging = 'war'
          }

          licenses {
            license {
              name = 'Copyright 2019 Linkedin, All Rights Reserved'
              url = 'https://github.com/linkedin/brooklin/blob/master/LICENSE'
            }
          }
          developers {
            developer {
              name = 'Brooklin Development Team'
              email = 'brooklin-oss@linkedin.com'
              organization = 'LinkedIn'
              organizationUrl = 'linkedin.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com:linkedin/brooklin.git'
            developerConnection = 'scm:git:ssh://github.com:linkedin/brooklin.git'
            url = 'https://github.com/linkedin/brooklin'
          }
        }
      }

      dataTemplateJava(MavenPublication) {
        artifactId project.name + "-data-template"
        artifact mainDataTemplateJar
        artifact dataTemplateSourceJar
        artifact javadocJar

        version project.version
      }

      testDataTemplateJava(MavenPublication) {
        artifactId project.name + "-test-data-template"
        artifact testDataTemplateJar
        artifact testDataTemplateSourceJar
        artifact javadocJar

        version project.version
      }
    }
  }

  bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = allPublications
    pkg {
      repo = 'maven'
      name = 'brooklin'
      userOrg = 'linkedin'
      licenses = ['BSD 2-Clause']
      vcsUrl = 'https://github.com/linkedin/brooklin'
      publicDownloadNumbers = true
      version {
        name = project.version
      }
      publish = false
      override = false
    }
  }
}
